//
//  This shows the basic structure of a modal dialog. It explains where the controls go and how to
//  initialize it (PopupTheDialog & OnShow). The DialogResult  method explains how to return data
//  from your dialog. The commented oOpenDialog_btn is an example of a button that opens this dialog.
//  This button would be placed on another view.
//
//    Object oOpenDialog_btn is a cWebButton
//        Set psCaption to "Open Dialog"
//
//        Procedure OnClick
//            Send PopupTheDialog of oWebModalDialog Self "Param 1" "Param 2"
//        End_Procedure
//
//        Procedure OnCloseModalDialog Handle hoModalDialog
//            String sResult
//
//            Get DialogResult of hoModalDialog to sResult
//        End_Procedure
//    End_Object
//


Use cWebModalDialog.pkg
Use cWebPanel.pkg
Use cWebButton.pkg
Use cWebForm.pkg
Use cJsonHttpTransfer.pkg
Use cWebGroup.pkg

Object oDemoDisplayUser is a cWebModalDialog
    Set psCaption to "Display User"
    Set piMinWidth to 500
    Set piMinHeight to 200
    Set pbServerOnSubmit to True    // enable the OnSubmit event
    
    Object oJsonHttp is a cJsonHttpTransfer
        // properties to set to access an HTTPS site
        Set piRemotePort to rpHttpSSL
        Set peTransferFlags to ifSecure
    End_Object
    
    Object oMainPanel is a cWebPanel
        Set piColumnCount to 12
        
        Object oIdFrm is a cWebForm
            Set piColumnSpan to 0
            Set psLabel to "Id"
            Set peDataType to typeNumber
            Set pbReadOnly to True
        End_Object
        
        Object oNameFrm is a cWebForm
            Set piColumnSpan to 0
            Set psLabel to "Name"
        End_Object
        
        Object oUsernameFrm is a cWebForm
            Set piColumnSpan to 0
            Set psLabel to "Username"
        End_Object
        
        Object oEmailFrm is a cWebForm
            Set piColumnSpan to 0
            Set psLabel to "Email"
        End_Object
        
        Object oPhoneFrm is a cWebForm
            Set piColumnSpan to 0
            Set psLabel to "Phone"
        End_Object
        
        Object oCompanyGrp is a cWebGroup
            Set psCaption to "Company"
            Set piColumnCount to 12
            Set psBackgroundColor to "inherit"
            
            Object oCompanyNameFrm is a cWebForm
                Set piColumnSpan to 0
                Set psLabel to "Name"
            End_Object
            
            Object oCatchPhraseFrm is a cWebForm
                Set piColumnSpan to 0
                Set psLabel to "Catch phrase"
            End_Object
        End_Object
        
        
    End_Object
    
    Object oBottomPanel is a cWebPanel
        Set piColumnCount to 6
        Set peRegion to prBottom
        
        Object oOkButton is a cWebButton
            Set psCaption to "Update"
            Set piColumnSpan to 1
            Set piColumnIndex to 4
            
            Procedure OnClick
                Send Ok
            End_Procedure
        End_Object
        
        Object oCancelButton is a cWebButton
            Set psCaption to C_$Cancel
            Set piColumnSpan to 1
            Set piColumnIndex to 5
            
            Procedure OnClick
                Send Cancel
            End_Procedure
        End_Object
    End_Object
    
    Procedure OnSubmit
        Send Ok
    End_Procedure
    
    Procedure Ok
        Handle hoJsonObj hoCompany hoResponse
        String sVal sId sErr
        Boolean bOk
        
        //  Build JSON Patch doc
        Get Create (RefClass(cJsonObject)) to hoJsonObj
        
        Send InitializeJsonType of hoJsonObj jsonTypeObject
        
        WebGet psValue of oNameFrm to sVal
        Send SetMemberValue of hoJsonObj "name" jsonTypeString sVal
        
        WebGet psValue of oUsernameFrm to sVal
        Send SetMemberValue of hoJsonObj "username" jsonTypeString sVal
        
        WebGet psValue of oEmailFrm to sVal
        Send SetMemberValue of hoJsonObj "email" jsonTypeString sVal
        
        WebGet psValue of oPhoneFrm to sVal
        Send SetMemberValue of hoJsonObj "phone" jsonTypeString sVal
        
        Get Create (RefClass(cJsonObject)) to hoCompany
        Send InitializeJsonType of hoCompany jsonTypeObject
        
        WebGet psValue of oCompanyNameFrm to sVal
        Send SetMemberValue of hoCompany "name" jsonTypeString sVal
        
        WebGet psValue of oCatchPhraseFrm to sVal
        Send SetMemberValue of hoCompany "catchPhrase" jsonTypeString sVal
        
        Send SetMember of hoJsonObj "company" hoCompany
        Send Destroy of hoCompany
        
        WebGet psValue of oIdFrm to sId
        
        //  Send PATCH request
        Get HttpPatchJson of oJsonHttp "jsonplaceholder.typicode.com" ("/users/" + sId) hoJsonObj (&bOk) to hoJsonObj
        
        Send Destroy of hoJsonObj
        
        If (bOk) Begin
            Forward Send Ok
        End
        Else Begin
            Get TransferErrorDescription of oJsonHttp to sErr
            Send UserError sErr "Transfer failed!"
        End
    End_Procedure
    
    Procedure Cancel
        Forward Send Cancel
    End_Procedure
    
    Procedure DisplayUser Handle hoJsonObj
        String sVal
        Handle hoComp
        
        Get MemberValue of hoJsonObj "id" to sVal
        WebSet psValue of oIdFrm to sVal
        
        Get MemberValue of hoJsonObj "name" to sVal
        WebSet psValue of oNameFrm to sVal
        
        Get MemberValue of hoJsonObj "username" to sVal
        WebSet psValue of oUsernameFrm to sVal
        
        Get MemberValue of hoJsonObj "email" to sVal
        WebSet psValue of oEmailFrm to sVal
        
        Get MemberValue of hoJsonObj "phone" to sVal
        WebSet psValue of oPhoneFrm to sVal
        
        Get Member of hoJsonObj "company" to hoComp
        If (hoComp) Begin
            
            Get MemberValue of hoComp "name" to sVal
            WebSet psValue of oCompanyNameFrm to sVal
            
            Get MemberValue of hoComp "catchPhrase" to sVal
            WebSet psValue of oCatchPhraseFrm to sVal
            
            Send Destroy of hoComp
        End
    End_Procedure
    
    Procedure PopupUserDialog  Handle hReturnObj Integer iUserId
        Boolean bOk
        Handle hoJson
        String sErr
        
        //  GET the user
        Get HttpGetJson of oJsonHttp "jsonplaceholder.typicode.com" ("/users/" + String(iUserId)) (&bOk) to hoJson
        
        If (bOk) Begin
            Get IsMemberOfJsonType of hoJson "id" jsonTypeInteger to bOk
            If (bOk) Begin
                Send DisplayUser hoJson
                
                
                // Invoke the modal dialog
                Send Popup hReturnObj
            End
            Else Begin
                Send UserError "User does not exist!"
            End
            
            Send Destroy of hoJson
        End
        Else Begin
            Get TransferErrorDescription of oJsonHttp to sErr
            Send UserError sErr "Transfer failed!"
        End
        
        
    End_Procedure
    
End_Object
