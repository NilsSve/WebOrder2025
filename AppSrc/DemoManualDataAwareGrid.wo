Use cWebView.pkg
Use cWebPanel.pkg
Use cWebGrid.pkg
Use cWebColumn.pkg

Use cVendorDataDictionary.dd
Use cInventoryDataDictionary.dd

Object oDemoManualDataAwareGrid is a cWebView
    Set piColumnCount to 10
    Set psCaption to "Manual Data Aware Grid"
    Set piWidth to 750
    
    Object oVendorDataDictionary is a cVendorDataDictionary
    End_Object
    
    Object oInventoryDataDictionary is a cInventoryDataDictionary
        Set DDO_Server to oVendorDataDictionary
    End_Object
    
    Set Main_DD to oInventoryDataDictionary
    Set Server  to oInventoryDataDictionary
    
    Object oGrid is a cWebGrid
        Set piHeight to 500
        Set pbColumnSortable to True
        Set peDbGridType to gtManual
        
        Object oInventory_Description is a cWebColumn
            Entry_Item Inventory.Description
            Set psCaption to "Inventory Item"
            Set piWidth to 75
            
            Set pbSortable to True
        End_Object
        
        Object oInventory_Vendor_Part_ID is a cWebColumn
            Entry_Item Inventory.Vendor_Part_ID
            Set psCaption to "Part ID"
            Set piWidth to 40
            
            Set pbSortable to True
        End_Object
        
        Object oVendor_ID is a cWebColumn
            Entry_Item Vendor.ID
            Set psCaption to "Vendor"
            Set piWidth to 30
            
            Set pbSortable to True
        End_Object
        
        Object oVendor_Name is a cWebColumn
            Entry_Item Vendor.Name
            Set psCaption to "Vendor"
            Set piWidth to 50
            
            Set pbSortable to True
        End_Object
        
        Object oInventory_On_Hand is a cWebColumn
            Entry_Item Inventory.On_Hand
            Set psCaption to "On Hand"
            Set piWidth to 20
            
            Set pbSortable to True
        End_Object
        
        Object oInventory_Unit_Price is a cWebColumn
            Entry_Item Inventory.Unit_Price
            Set psCaption to "Unit Price"
            Set piWidth to 30
            
            Set pbSortable to True
        End_Object
        
        Procedure OnManualLoadData tWebRow[] ByRef aTheRows String ByRef sCurrentRowID
            // Loads the first 10 inventory items.
            Handle hoDD
            RowID riRowID
            String sRowID
            Integer iRow
            
            // Establish the find ordering and find the first record.
            Move 0 to iRow
            Get Server to hoDD
            Get ReadDDFirstRecord hoDD 1 False to riRowID
            
            //  Find remaining records....
            While (Found and (iRow < 10))
                //  We use the LoadGridRow function to fill the row struct based on the data entry object (DEO) values
                Get LoadGridRow to aTheRows[iRow]
                
                Increment iRow
                Send Locate_Next to hoDD
            Loop
            
            // Set the first row to be the selected row
            If (IsNullRowID(riRowID)) Begin
                Move "" to sCurrentRowID
            End
            Else Begin
                Send FindByRowId of oInventoryDataDictionary Inventory.File_Number riRowID
                Move (SerializeRowID(riRowID)) to sCurrentRowID
            End
            
        End_Procedure
        
        Procedure OnLoad
            Send GridRefresh
        End_Procedure
    End_Object
    
End_Object

