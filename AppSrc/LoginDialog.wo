Use cWebModalDialog.pkg
Use cWebForm.pkg
Use cWebButton.pkg
Use cWebPanel.pkg
Use cWebLabel.pkg

Object oLoginDialog is a cWebModalDialog
    Set piMinWidth to 360
    Set piMinHeight to 200
    Set psCaption to "Login"
    Set pbShowClose to (peLoginMode(oWebApp) <> lmLoginRequired)
    Set pbLoginModeEnforced to False
    
    Delegate Set phoLoginView to Self
    
    Object oTopPanel is a cWebPanel
        Set peRegion to prTop
        
        Object oDescription is a cWebLabel
            Set psCaption to "Please enter your User ID and Password to login. (Hint: John, John)"
            Set peAlign to alignCenter
        End_Object
    End_Object
    
    Object oRightPanel is a cWebPanel
        Set peRegion to prRight
        Set piWidth to 20
    End_Object
    
    Object oMainPanel is a cWebPanel
        Set peRegion to prCenter
        
        Object oLoginName is a cWebForm
            Set psLabel to "User ID:"
            Set piMaxLength to 20
            Set psAutoComplete to "username"
            Set peLabelPosition to lpFloat
        End_Object
        
        Object oPassword is a cWebForm
            Set psLabel to "Password:"
            Set pbPassword to True
            Set piMaxLength to 20
            Set psAutoComplete to "current-password"
            Set peLabelPosition to lpFloat
        End_Object
    End_Object
    
    Object oButtonPanel is a cWebPanel
        Set peRegion to prBottom
        Set piColumnCount to 3
        
        Object oWarning is a cWebLabel
            Set pbVisible to False
            Set psCaption to "Invalid User ID or password. Hint: 'John' & 'John'."
            Set psCSSClass to "Warning"
            Set peAlign to alignCenter
            Set piColumnSpan to 3
        End_Object
        
        Object oLoginButton is a cWebButton
            Set pbShowLabel to False
            Set piColumnIndex to 1
            Set psCaption to "Login"
            Set pbServerOnClick to True
            
            Procedure OnClick
                Send DoLogin
            End_Procedure
        End_Object
        
    End_Object
    
    Procedure DoLogin
        String sLoginName sPassword sStateHash
        Boolean bResult
        Handle hoDefaultView
        
        WebGet psValue of oLoginName to sLoginName
        WebGet psValue of oPassword to sPassword
        
        Get UserLogin of ghoWebSessionManager sLoginName sPassword to bResult
        
        If (bResult) Begin
            Send Hide of oLoginDialog
            
            Get StateHash of ghoWebApp to sStateHash
            
            If (sStateHash <> "") Begin
                Send RestoreState sStateHash
            End
            Else Begin
                Get GetDefaultView to hoDefaultView
                If (hoDefaultView > 0) Begin
                    Send Show of hoDefaultView
                End
            End
            
            // clear the login values. we don't want to return the login id & password as synchronized properties....
            WebSet psValue of oLoginName to ""
            WebSet psValue of oPassword  to ""
            WebSet pbVisible of oWarning to False
        End
        Else Begin
            WebSet pbVisible of oWarning to True
        End
    End_Procedure
    
    
    Set pbServerOnSubmit to True
    Procedure OnSubmit
        Send DoLogin
    End_Procedure
End_Object



