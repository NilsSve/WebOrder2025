Use cWebModalDialog.pkg
Use cWebPanel.pkg
Use cWebButton.pkg
Use cWebForm.pkg
Use cWebMultiSelectList.pkg
Use cWebColumnSelectionIndicator.pkg

Use cCustomerDataDictionary.dd
Use cSalesPersonDataDictionary.dd
Use cOrderHeaderDataDictionary.dd
Use cVendorDataDictionary.dd
Use cInventoryDataDictionary.dd

Object oOrderItemSelect is a cWebModalDialog
    Object oVendor_DD is a cVendorDataDictionary
    End_Object

    Object oInventory_DD is a cInventoryDataDictionary
        Set DDO_Server to oVendor_DD
    End_Object

    Set Main_DD to oInventory_DD
    Set Server to oInventory_DD

    Set psCaption to "Select Order Items..."
    Set piWidth to 700
    Set piHeight to 400
    
    Object oMainPanel is a cWebPanel
        Set piColumnCount to 12

        Object oPromptList is a cWebMultiSelectList
            Set pbFillHeight to True
            Set piColumnSpan to 12
            Set pbSelectionByRow to False
            
            Object oMultiSelect is a cWebColumnSelectionIndicator
                Set piWidth to 86
            End_Object
            
            Object oItem_ID is a cWebColumn
                Entry_Item Inventory.Item_ID
                Set psCaption to "Item Id"
                Set piWidth to 151
            End_Object
            
            Object oItem_Description is a cWebColumn
                Entry_Item Inventory.Description
                Set psCaption to "Name"
                Set piWidth to 380
            End_Object
            
            Object oItem_Vendor is a cWebColumn
                Entry_Item Vendor.Name
                Set psCaption to "Vendor"
                Set piWidth to 380
            End_Object
        End_Object
        
    End_Object 
    
    Object oBottomPanel is a cWebPanel
        Set piColumnCount to 6
        Set peRegion to prBottom

        Object oOkButton is a cWebButton
            Set psCaption to C_$OK
            Set piColumnSpan to 1
            Set piColumnIndex to 4

            Procedure OnClick
                Send Ok
            End_Procedure
        End_Object 

        Object oCancelButton is a cWebButton
            Set psCaption to C_$Cancel
            Set piColumnSpan to 1
            Set piColumnIndex to 5

            Procedure OnClick
                Send Cancel
            End_Procedure
        End_Object 
    End_Object
    
    Procedure PopupOrderItemSelect  Handle hReturnObj
        // Invoke the modal dialog    
        Send Popup hReturnObj
        
        // Clear Possible Previous Selection
        Send ClearListSelection of oPromptList
        
        // Search
        Send FindFromTop of oPromptList
    End_Procedure
    
    Function DialogResult Returns String[]
        String[] aSelectedItems
        Get ListSelection of oPromptList to aSelectedItems
        Function_Return aSelectedItems
    End_Function

End_Object