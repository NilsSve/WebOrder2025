Use cWebService.pkg
Use DataDict.pkg

Use cVENDORDataDictionary.dd
Use cInventoryDataDictionary.dd
Use cCUSTOMERDataDictionary.dd
Use cSalesPersonDataDictionary.dd
Use cOrderHeaderDataDictionary.dd
Use cOrderDetailDataDictionary.dd

Object oTestService is a cWebService
    
    Set psDocumentation to ;
        ("This is a Test Web Service. It contains a few simple operations " +;
        "demonstrating how to create a DataFlex Web Service. "+;
        "To test any of these web services using your web browser, simply follow "+;
        "the link below for the specified service. "+;
        "You can also view the WSDL for this service by accessing the link to "+;
        "Service Description below. To create a new Web Service operation, simply "+;
        "add a function in the oTestService Web Service object and select Publish. "+;
        "To learn more about creating Web Services in DataFlex, please see the "+;
        "Web Services section in the DataFlex Help.")
    
    Set psServiceName to "TestService"
    Set psServiceURI to "http://www.dataaccess.com/examples/testservice"
    Set psServiceTitle to "DataFlex Test Web Service"
    Set psDescription to "Test Web Service"
    
    Object oVendorDataDictionary is a cVendorDataDictionary
        Send DefineAllExtendedFields
    End_Object
    
    Object oInventoryDataDictionary is a cInventoryDataDictionary
        Set DDO_Server to oVendorDataDictionary
        Send DefineAllExtendedFields
    End_Object
    
    Object oCustomerDataDictionary is a cCustomerDataDictionary
        Send DefineAllExtendedFields
    End_Object
    
    Object oSalesPersonDataDictionary is a cSalesPersonDataDictionary
        Send DefineAllExtendedFields
    End_Object
    
    Object oOrderHeaderDataDictionary is a cOrderHeaderDataDictionary
        Set DDO_Server to oCustomerDataDictionary
        Set DDO_Server to oSalesPersonDataDictionary
        Send DefineAllExtendedFields
    End_Object
    
    Object oOrderDetailDataDictionary is a cOrderDetailDataDictionary
        Set DDO_Server to oOrderHeaderDataDictionary
        Set DDO_Server to oInventoryDataDictionary
        Send DefineAllExtendedFields
    End_Object
    
    Set Main_DD to oInventoryDataDictionary
    
    { Published = True  }
    { Description = "Returns a hello message using the name passed as the string."  }
    Function SayHello String sName Returns String
        String sReturn
        
        Move ("Hello," * sName +".") to sReturn
        
        Function_Return sReturn
    End_Function
    
    { Published = True }
    { Description = "Returns a welcome message using the name passed as the string and IIS server variables." }
    Function Welcome String sName Returns String
        String sReturn
        String sIPAddr sServer sLocalAddr
        
        Get ServerVariable "REMOTE_ADDR" to sIpAddr
        Get ServerVariable "SERVER_NAME" to sServer
        Get ServerVariable "LOCAL_ADDR" to sLocalAddr
        
        Move ("Hello," * sName + ". This is" * sServer) to sReturn
        Move (sReturn * ". I see you're from" * sIPAddr * ". Welcome to" * sLocalAddr +"!") to sReturn
        
        Function_Return sReturn
    End_Function
    
    { Published = True }
    { Description = "Echoes back the string passed." }
    Function Echo String echoString Returns String
        Function_Return echoString
    End_Function
    
    { Published = True }
    { Description = "Adds two numbers and returns the result." }
    Function AddNumber Real number1 Real number2 Returns Real
        Function_Return (number1+number2)
    End_Function
    
    { Published = True }
    { Description = "Looks up the price of a particular inventory item. Example item identifiers, DT, GOLD, MAPS, OBM, RUNMTR." }
    Function PriceQuote String itemID Returns Number
        Send Clear of oInventoryDataDictionary
        Move itemID to Inventory.Item_id
        Send Find of oInventoryDataDictionary eq 1
        If (Not(Found)) Begin
            //If the inventory item cannot be found, raise a web service exception
            Send WebServiceException ("Unknown inventory item '"+itemID+"'")
            Function_Return
        End
        Function_Return Inventory.Unit_price
    End_Function
    
    { Published = True }
    { Description = "Looks up an order and returns the delivery date. Example: customer number 1 and order number 101." }
    Function EstimatedOrderDeliveryDate Integer customerNumber Integer orderNumber Returns Date
        Send Clear of oOrderHeaderDataDictionary
        Move customerNumber to OrderHeader.Customer_number
        Move orderNumber to OrderHeader.Order_number
        Send Find of oOrderHeaderDataDictionary eq 2
        If (Not(Found)) Begin
            //If the order cannot be found, we raise a web service exception
            Send WebServiceException "The specified order number, or order number/customer number combination, is invalid"
            Function_Return
        End
        
        //We don't actually have a delivery date field in the database
        //so we'll simply use the order date for this sample and add 30 days to it
        Function_Return (OrderHeader.Order_Date + 30)
    End_Function
    
    { Published = True }
    { Description = "Calculates the total number of items sold. Example: item identifiers, DT, GOLD, MAPS, OBM, RUNMTR." }
    Function ItemsSoldToDate String itemID Returns Integer
        Integer total
        //We'll do a simple brute force search through the database
        //and calculate the total number of items sold.
        //It's a bit naive, but it will do for this example
        Send Clear of oOrderDetailDataDictionary
        Send Find of oOrderDetailDataDictionary ge 1
        While (Found)
            If (OrderDetail.Item_id = Uppercase(itemId));
                Increment total
            Send Find of oOrderDetailDataDictionary gt 1
        Loop
        Function_Return total
    End_Function
    
End_Object

